cmake_minimum_required(VERSION 2.8.2)

project(OpenSSL-download NONE)
include(ExternalProject)

set(EP_SOURCE_DIR ${CMAKE_BINARY_DIR}/openssl-src)
set(EP_BINARY_DIR ${CMAKE_BINARY_DIR}/openssl-src)
set(EP_INSTALL_DIR ${CMAKE_INSTALL_PREFIX})

set(openssl_TAG 1.0.2u)
set(openssl_SHA256 ecd0c6ffb493dd06707d38b14bb4d8c2288bb7033735606569d8f90f89669d16)
set(openssl_URL "http://www.openssl.org/source/old/1.0.2/openssl-${openssl_TAG}.tar.gz")

include(ProcessorCount)
ProcessorCount(CPU_COUNT)
if(CPU_COUNT EQUAL 0)
  set(CPU_COUNT 2)
endif()

if (WIN32)
  set(OPENSSL_PATCH_COMMAND git apply --ignore-space-change --ignore-whitespace ${CMAKE_SOURCE_DIR}/bcryptgen.patch)
  set(OPENSSL_CONFIG_COMMAND perl configure VC-WIN64A CC=cl RC=rc --prefix=${EP_INSTALL_DIR} --openssldir=${EP_INSTALL_DIR})
  set(OPENSSL_BUILD_COMMAND nmake)
  set(OPENSSL_INSTALL_COMMAND nmake install)
else()
  set(OPENSSL_PATCH_COMMAND "")
  set(OPENSSL_CONFIG_COMMAND ./config --prefix=${EP_INSTALL_DIR})
  set(OPENSSL_BUILD_COMMAND make -j ${CPU_COUNT})
  set(OPENSSL_INSTALL_COMMAND make install_sw)
endif()

ExternalProject_Add(openssl
    URL ${openssl_URL}
    URL_HASH SHA256=${openssl_SHA256}
    SOURCE_DIR ${EP_SOURCE_DIR}
    BINARY_DIR ${EP_BINARY_DIR}
    INSTALL_DIR ${EP_INSTALL_DIR}
    PATCH_COMMAND ${OPENSSL_PATCH_COMMAND}
    CONFIGURE_COMMAND ${OPENSSL_CONFIG_COMMAND}
    BUILD_COMMAND ${OPENSSL_BUILD_COMMAND}
    INSTALL_COMMAND ${OPENSSL_INSTALL_COMMAND}
)

